function setupIndy()
{
	// Setup actor, costume, and animations
	VAR_EGO = ACTOR_INDY;
	Actor_setCurrent(VAR_EGO);
	Actor_setCostume(indyCostume);
	Actor_setInitAnim(indyCostume_stand);
	Actor_setStandAnim(indyCostume_stand);
	Actor_setWalkAnim(indyCostume_walk);
	Actor_setTalkAnim(indyCostume_talk, indyCostume_stand);
	Actor_setStepDistance(2, 1);
	Actor_ignoreBoxes(1);
	Actor_setScale(0xFF);
}

function setupCharset()
{
	Interface_setCharset(font2);
	Interface_setCharsetColors([0, 0, 0, 0], 4);
	Actor_setTalkColor(COLOR_WHITE);
	Actor_printLoadDefault(VAR_EGO);
	Actor_printCenter();
	Actor_printOverhead();
	Actor_printSaveDefault();
}

function entry()
{
	setupCharset();
	setupIndy();
	
	// Print faked noise text
	Interface_printLineLoadDefault();
	Interface_printLineColor(COLOR_GORSE);
	Interface_printLineAt(220, 170);
	Interface_printLine("*cling*");
	
	// Set Indy out of the screen and say something
	Actor_putAtXY(VAR_EGO, 205, 300, VAR_ROOM);
	Script_delaySeconds(1);
	Actor_talkEgo("Shoot! Another broken pick axe!");
	Script_waitForMessage();
	Script_delaySeconds(1);

	// Climb out of hole
	Actor_setCostume(indyClimbingCostume);
	Actor_setAnimSpeed(6);
	Actor_putAtXY(VAR_EGO, 205, 193, VAR_ROOM);
	Actor_setDirection(DIR_SOUTH);
	Actor_animate(VAR_EGO, indyClimbingCostume_anim);
	Script_delaySeconds(3);

	// Restore costume
	Actor_setCostume(indyCostume);
	Actor_setAnimSpeed(4);

	// Walk and say something
	Actor_walkToXY(VAR_EGO, 250, 195);
	Script_waitForActor(VAR_EGO);
	Actor_talkEgo("Well, I guess I'll have to find another one...");
}

function exit()
{
}